project ('com.github.jeremypw.gnonograms', 'vala', 'c')
shortname = 'gnonograms'
domain = 'com.github.jeremypw.'
schema_path = '/com/github/jeremypw/' + shortname + '/'
tmp_dir_path = '/.local/share/' + shortname + '/'
libver = '1.3'
libname = shortname + '-core'

i18n = import ('i18n')

add_global_arguments (
    '-DGETTEXT_PACKAGE="' + meson.project_name () + '"',
    language:'c'
)

dependency('gtk+-3.0', version: '>=3.18', required: true)
GTK322 = dependency('gtk+-3.0', version: '>=3.22', required: false)

if GTK322.found ()
    add_project_arguments (
        '--define=HAVE_GTK_3_22',
        language:'vala'
    )
endif

lib = shared_library (
    libname,
    'libcore/widgets/Labelbox.vala',
    'libcore/widgets/Label.vala',
    'libcore/widgets/Cellgrid.vala',
    'libcore/AbstractPatternGenerator.vala',
    'libcore/AbstractGameGenerator.vala',
    'libcore/AbstractSolver.vala',
    'libcore/utils.vala',
    'libcore/Model.vala',
    'libcore/My2DCellArray.vala',
    'libcore/Range.vala',
    'libcore/Region.vala',
    'libcore/Solver.vala',
    'libcore/Filereader.vala',
    'libcore/Filewriter.vala',
    'libcore/Move.vala',
    'libcore/History.vala',
    'libcore/Enums.vala',
    'libcore/Structs.vala',
    'libcore/Constants.vala',

    dependencies : [
        dependency ('gtk+-3.0'),
        dependency ('gee-0.8'),
        dependency ('granite')
    ],

    install: true,
    install_dir: [true, true, true],
    version: libver,
    soversion: '0'
)

pkg = import ('pkgconfig')
pkg.generate (
    description: 'Core functions for gnonograms and gnonogram-tools',
    filebase: 'gnonograms-core',
    libraries: lib,
    name: libname,
    version: libver,
    requires: ['gtk+-3.0', 'gee-0.8', 'granite']
)

executable (
    meson.project_name (),
    'src/Application.vala',
    'src/Controller.vala',
    'src/View.vala',
    'src/RandomPatternGenerator.vala',
    'src/SimpleRandomGameGenerator.vala',
    'src/widgets/AppMenu.vala',
    'src/widgets/ViewModeButton.vala',
    'src/widgets/Progress_indicator.vala',

    dependencies : [
        dependency ('gtk+-3.0'),
        dependency ('gee-0.8'),
        dependency ('granite')
    ],

    install: true,
    link_with: lib
)


meson.add_install_script('meson/post_install.py')

uninstall_data = configuration_data()
uninstall_data.set('schema_install_dir', join_paths(get_option('prefix'), get_option('datadir'), 'glib-2.0', 'schemas'))
uninstall_data.set('schema_path', schema_path)
uninstall_data.set('tmp_dir_path', tmp_dir_path)
uninstall_data.set('icon_dir_path', join_paths(get_option('prefix'), get_option('datadir'), 'icons', 'hicolor'))
uninstall_data.set('mime_dir_path', join_paths(get_option('prefix'), get_option('datadir'), 'mime', 'packages'))
uninstall_data.set('desktop_dir_path', join_paths(get_option('prefix'), get_option('datadir'), 'applications'))

configure_file (
    input: join_paths (meson.source_root (), 'data', 'post_uninstall.py.in'),
    output: 'post_uninstall.py',
    configuration: uninstall_data
)

subdir ('po')
subdir ('data')
